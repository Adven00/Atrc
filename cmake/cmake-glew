INCLUDE(ExternalProject)

FILE(WRITE ${CMAKE_BINARY_DIR}/glew-CMakeLists.txt
    "CMAKE_MINIMUM_REQUIRED(VERSION ${CMAKE_VERSION})\n"
    "FILE(APPEND auto/blacklist \"NV/draw_vulkan_image\")\n"
    "EXECUTE_PROCESS(\n"
    "   WORKING_DIRECTORY \${CMAKE_CURRENT_SOURCE_DIR}/auto\n"
    "   COMMAND \${CMAKE_MAKE_PROGRAM}\n"
    ")\n"
    "ADD_SUBDIRECTORY(build/cmake)\n"
)

SET(GLEW_INSTALL_DIR "${CMAKE_BINARY_DIR}/glew_install")

ExternalProject_Add(glew
	PREFIX "${PROJECT_SOURCE_DIR}/lib/glew"
    URL "https://github.com/nigels-com/glew/releases/download/glew-2.1.0/glew-2.1.0.zip"
    URL_MD5 "dff2939fd404d054c1036cc0409d19f1"
    UPDATE_COMMAND ""
	PATCH_COMMAND
		${CMAKE_COMMAND} -E copy
			${CMAKE_BINARY_DIR}/glew-CMakeLists.txt
			${PROJECT_SOURCE_DIR}/lib/glew/src/glew/CMakeLists.txt
    CMAKE_ARGS
        "-DCMAKE_INSTALL_PREFIX=${GLEW_INSTALL_DIR}"
        "-DCMAKE_BUILD_TYPE=Release"
    CMAKE_CACHE_ARGS
        "-DCMAKE_C_COMPILER:FILEPATH=${CMAKE_C_COMPILER}"
        "-DCMAKE_CXX_COMPILER:FILEPATH=${CMAKE_CXX_COMPILER}"
    LOG_DOWNLOAD 1
	LOG_UPDATE 1
	LOG_CONFIGURE 1
	LOG_BUILD 1
	LOG_INSTALL 1
)

SET(GLEW_INCLUDE_DIRS ${GLEW_INSTALL_DIR}/include)
SET(GLEW_LIBRARIES ${GLEW_INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}glew32${CMAKE_STATIC_LIBRARY_SUFFIX})

SET(GLEW_INCLUDE_DIRS ${GLEW_INCLUDE_DIRS} CACHE STRING "")
SET(GLEW_LIBRARIES ${GLEW_LIBRARIES} CACHE STRING "")
