SET(OIDN_TRIGGER_CONTENT "
	CMAKE_MINIMUM_REQUIRED(VERSION 3.10)
	INCLUDE(ExternalProject)
	PROJECT(OIDNDownloader)
	SET(OIDN_VERSION \"1.0.0\")
	IF(WIN32)
		SET(OIDN_URL https://github.com/OpenImageDenoise/oidn/releases/download/v1.0.0/oidn-1.0.0.x64.vc14.windows.zip)
		SET(OIDN_URL_MD5 0e9bbcd03239777dd46d0f79f5da7d8a)
	ELSE()
		SET(OIDN_URL https://github.com/OpenImageDenoise/oidn/releases/download/v1.0.0/oidn-1.0.0.x86_64.linux.tar.gz)
		SET(OIDN_URL_MD5 aa4e637391b55cb711199a24d844d9d2)
	ENDIF()
	ExternalProject_Add(
		OIDN
		PREFIX \"${PROJECT_SOURCE_DIR}/lib/oidn\"
		URL \"\${OIDN_URL}\"
		URL_MD5 \"\${OIDN_URL_MD5}\"
		UPDATE_COMMAND	  \"\"
		CONFIGURE_COMMAND \"\"
		BUILD_COMMAND	  \"\"
		INSTALL_COMMAND	  \"\"
	)
	ADD_CUSTOM_TARGET(OIDNTrigger)
	ADD_DEPENDENCIES(OIDNTrigger OIDN)
")

FILE(MAKE_DIRECTORY ${CMAKE_BINARY_DIR}/OIDNTrigger ${CMAKE_BINARY_DIR}/OIDNTrigger/build)
FILE(WRITE ${CMAKE_BINARY_DIR}/OIDNTrigger/CMakeLists.txt "${OIDN_TRIGGER_CONTENT}")
EXECUTE_PROCESS(COMMAND ${CMAKE_COMMAND} .. WORKING_DIRECTORY ${CMAKE_BINARY_DIR}/OIDNTrigger/build)
   EXECUTE_PROCESS(COMMAND ${CMAKE_COMMAND} --build . WORKING_DIRECTORY ${CMAKE_BINARY_DIR}/OIDNTrigger/build)
	
SET(OIDN_FIND_PATH "${PROJECT_SOURCE_DIR}/lib/oidn/src/OIDN/lib/cmake/OpenImageDenoise")
FIND_PACKAGE(OpenImageDenoise PATHS ${OIDN_FIND_PATH})
